
buildscript {

    ext {
        springBootVersion = '1.3.2.RELEASE'
        kotlinVersion = '1.0.0-beta-4589'
        // see https://repo1.maven.org/maven2/org/jetbrains/kotlin/kotlin-stdlib/ for version updates...
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin"
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}


apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'org.asciidoctor.convert'


jar {
    baseName = 'jobroom-api'
    version = '0.2.0-SNAPSHOT'

    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()

    // due to restdocs SNAPSHOT
    maven {
        url 'http://repo.spring.io/snapshot'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

    // jackson-module-kotlin is required for handling immutable objects (e.g. without typical java bean setters)
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:${dependencyManagement.compile.importedProperties['jackson.version']}"

    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile "org.springframework.boot:spring-boot-devtools"
    // compile "org.springframework.boot:spring-boot-starter-jooq"
    // compile "org.springframework.boot:spring-boot-starter-security"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "junit:junit"
    testCompile "com.jayway.jsonpath:json-path:0.8.1"
//    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RC1'
    // error java.lang.IllegalAccessError with 1.0.0 RC1
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.2.BUILD-SNAPSHOT'

    runtime 'org.hsqldb:hsqldb'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}
